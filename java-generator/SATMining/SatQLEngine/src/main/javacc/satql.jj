/* satql.jj - JavaCC Grammar for the SATQL language.

   Copyright (C) 2013, 2014 Emmanuel Coquery.

This file is part of SATMiner

SATMiner is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 2, or (at your option)
any later version.

SATMiner is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with SATMiner; see the file COPYING.  If not, write to the
Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
02110-1301 USA.

Linking this library statically or dynamically with other modules is
making a combined work based on this library.  Thus, the terms and
conditions of the GNU General Public License cover the whole
combination.

As a special exception, the copyright holders of this library give you
permission to link this library with independent modules to produce an
executable, regardless of the license terms of these independent
modules, and to copy and distribute the resulting executable under
terms of your choice, provided that you also meet, for each linked
independent module, the terms and conditions of the license of that
module.  An independent module is a module which is not derived from
or based on this library.  If you modify this library, you may extend
this exception to your version of the library, but you are not
obligated to do so.  If you do not wish to do so, delete this
exception statement from your version. */

options {

}

PARSER_BEGIN(SATQLParser)
package dag.satmining.problem.satql.parser;

import dag.satmining.problem.satql.ast.*;
import dag.satmining.problem.satql.ast.sql.*;
import java.util.ArrayList;
import java.util.Collection; 
import dag.satmining.constraints.Literal;

@SuppressWarnings({"unused","serial"})
public class SATQLParser<L extends Literal<L>> {

}

PARSER_END(SATQLParser)

/** Comments starts with -- and finish at the end of the line */
SKIP:
{
  " "
| "\t"
| "\n"
| "\r"
| <"--" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
}


TOKEN [ IGNORE_CASE ]: /* KEYWORDS */
{
  < AND: "AND">
| < OR: "OR">
| < NOT: "NOT">
| < TRUE: "TRUE" >
| < EQ: "=" >
| < FORALL: "F"|"FORALL" >
| < EXISTS: "E"|"EXISTS" >
| < FORMULA: "FORMULA" >
| < ATLEAST: "AL"|"ATLEAST" >
| < ATMOST: "AM"|"ATMOST" >
| < PERCENT: "%"|"PERCENT" >
| < OF: "OF" >
| < SCOPE: "SCOPE" > 
| < WHERE: "WHERE" >
| < FIND: "FIND" >
| < OVER: "OVER" >
| < SUCH: "SUCH" >
| < THAT: "THAT" >
| < EMPTY: "EMPTY" >
| < INTERSECTION: "INTERSECTION" >
| < WITH: "WITH" >
| < SINGLETON: "SINGLETON" >
| < THEN: "THEN" >
| < MINIMIZE: "MINIMIZE" >
| < MAXIMIZE: "MAXIMIZE" >
| < LIMIT: "LIMIT" >
| < IF: "IF" >
| < IN: "IN" >
| < COLON: ":" >
}

TOKEN : /* LITERALS */
{
  < LITERAL:
        <DECIMAL_LITERAL> 
      | <STRING_LITERAL>
  >
|
  < #DECIMAL_LITERAL: (["0"-"9"]("."["0"-"9"])?)+ >
|
  < #STRING_LITERAL: "'" ("''"|~["'"])* "'" >
}

TOKEN : /* IDENTIFIERS */
{
  < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
|
  < #LETTER: ["_","a"-"z","A"-"Z"] >
|
  < #DIGIT: ["0"-"9"] >
}

TOKEN : /* ATTRIBUTE IDENTIFIERS */
{
  < ATT_IDENTIFIER: "$" < IDENTIFIER > >
} 

TOKEN : /* RAW SQL STATEMENT */
{
  < RAW_SQL_STATEMENT: "{" (~["{","}"] | "{{" | "}}" )* "}" >
}


/** Mining Statement. */

MiningQuery<L> MiningQuery():
{
  MiningQuery<L> query;
  Token t;
  Where where;
  MiningExpression expr;
}
{
  <FIND> { query = new MiningQuery<L>(); }
  t=<IDENTIFIER> { query.addSchemaVariable(t.image); }
  ("," t=<IDENTIFIER> { query.addSchemaVariable(t.image); } )*
  <OVER>
  t=<IDENTIFIER> { query.addAttribute(t.image); }
  ("," t=<IDENTIFIER> { query.addAttribute(t.image); } )*
  <SCOPE> FromExpr(query.getFrom()) 
  ( "," FromExpr(query.getFrom()) )*

  TupleQuantifierExpr(query.getFrom(),true)
  (TupleQuantifierExpr(query.getFrom(),false))*
  <SUCH> <THAT> expr=MiningExpr(query.getDict()) 
  { query.setSuchThat(expr); }
  ((  (<MINIMIZE> t=<IDENTIFIER> { query.minimize(t.image); } )
    | (<MAXIMIZE> t=<IDENTIFIER> { query.maximize(t.image); } )
  )
  ( ","
    ( (<MINIMIZE> t=<IDENTIFIER> { query.minimize(t.image); } )
    | (<MAXIMIZE> t=<IDENTIFIER> { query.maximize(t.image); } ) )
  )*)?
  (
    <LIMIT> t=<LITERAL> { query.setLimit(Long.parseLong(t.image)); }
    )?
  ";"
  { return query; }
}

MiningExpression MiningExpr(ASTDictionnary dict) : 
{ MiningExpression expr; }
{
  expr=QuantifierIfExpr(dict)
  { return expr; }
}

MiningExpression QuantifierIfExpr(ASTDictionnary dict):
{ 
  MiningExpression expr,expr2; 
  Token t,t2;
}
{
  ( <FORALL> t=<ATT_IDENTIFIER> 
    {dict.getAttributeVariable(t.image);} 
    < IN > t2=< IDENTIFIER > < COLON >
    expr=QuantifierIfExpr(dict) 
    { return dict.forall(t.image,t2.image, expr); } )
| ( <EXISTS> t=<ATT_IDENTIFIER> 
    {dict.getAttributeVariable(t.image);} 
    < IN > t2=< IDENTIFIER > < COLON >
    expr=QuantifierIfExpr(dict) 
    { return dict.exists(t.image,t2.image, expr); } )
| ( <IF> expr=QuantifierIfExpr(dict) <THEN> expr2=QuantifierIfExpr(dict)
  { return dict.or(dict.neg(expr),expr2); } )
| ( expr=OrExpr(dict)
    { return expr; } )
}

MiningExpression OrExpr(ASTDictionnary dict):
{ MiningExpression a, b = null; }
{
  a=AndExpr(dict) ( <OR> b=OrExpr(dict) )? 
  {
    if (b==null) {
        return a;
    } else {
        return dict.or(a,b);
    }   
  }
}


MiningExpression AndExpr(ASTDictionnary dict) :
{ MiningExpression a, b = null; }
{
  a=NegExpr(dict) ( <AND> b=AndExpr(dict) )? 
  {
    if (b==null) {
        return a;
    } else {
        return dict.and(a,b);
    }   
  }
}

MiningExpression NegExpr(ASTDictionnary dict) :
{ MiningExpression a; }
{
  (<NOT> a=NegExpr(dict) {return dict.neg(a);})
| (a=AtomicExpr(dict) {return a;})
}

MiningExpression AtomicExpr(ASTDictionnary dict) :
{
  Token t1, t2, tOp;
  MiningExpression e;
  
}
{
  (<TRUE> {return dict.tt();})
| (t1=< RAW_SQL_STATEMENT > {return dict.rawSQLDelegate(t1.image);})
| ((t1=<ATT_IDENTIFIER>|t1=< IDENTIFIER >) <EQ> (t2=<ATT_IDENTIFIER>|t2=< IDENTIFIER >) {return dict.attCmp(t1.image,t2.image);})
| (e=Singleton(dict) {return e;})
| (e=EmptyIntersection(dict) {return e;})
| ( "(" e=MiningExpr(dict) ")" {return e;} )
}

MiningExpression Singleton(ASTDictionnary dict) :
{
  Token t1;
}
{
  <SINGLETON> t1=<IDENTIFIER> {return Sugar.singleton(dict,t1.image);}
}

MiningExpression EmptyIntersection(ASTDictionnary dict) :
{
  Token t;
  Collection<String> sch = new ArrayList<String>();
}
{
  <EMPTY> <INTERSECTION> "(" t=<IDENTIFIER> {sch.add(t.image);} 
  ( "," t=<IDENTIFIER> {sch.add(t.image);})+ ")" 
  {return Sugar.emptyIntersection(dict,sch);}
}


void FromExpr(From from):
{
FromExpression query = null;
Token t, t2 = null;
}
{
  ( t=< RAW_SQL_STATEMENT > t2=<IDENTIFIER> { from.addQueryName(new RAWSQLQuery(t.image,true),t2.image); } )
| ( t=<IDENTIFIER> (t2=<IDENTIFIER> )? 
    { if (t2==null) { 
        from.addName(t.image); 
    } else {
        from.addQueryName(new Relation(t.image), t2.image);
    } } )
}

void TupleQuantifierExpr(From from, boolean isFirstQuantifier):
{ 
  Token t,t2,t3 = null;
  Token t4 = null;
  ArrayList<String> namesNUplet = new ArrayList<String>(); 
}
{ 
    (<FORALL> ForallQuantifier(from, isFirstQuantifier))
| ( <ATLEAST> t=<LITERAL>
		(<PERCENT> AtleastPercentQuantifier(from, isFirstQuantifier, t)
		| (AtleastNQuantifier(from, isFirstQuantifier, t))))
}

void ForallQuantifier(From from, boolean isFirstQuantifier):
{
  Token t = null;
  Token t2 = null;
  Token t3 = null;
  Token t4 = null;
  ArrayList<String> namesNUplet = new ArrayList<String>(); 
}
{ 
    (AjoutNamesNUplet(namesNUplet) | "("AjoutNamesNUplet(namesNUplet) ("," AjoutNamesNUplet(namesNUplet))+")") (<WHERE> t3=< RAW_SQL_STATEMENT >)? <COLON> (t4=< RAW_SQL_STATEMENT > < AND >)?
    { if (t3==null) { 
		if (t4==null) {
			if (namesNUplet.size()==0) {
    	    	from.addQuantifierName(new RAWSQLQuery("{0=0}",true),new RAWSQLQuery("{0=0}",true), namesNUplet,isFirstQuantifier,new Integer("-1"),false);
     		} else {
        		from.addQuantifierName(new RAWSQLQuery("{0=0}",true),new RAWSQLQuery("{0=0}",true), namesNUplet,isFirstQuantifier,new Integer("-1"),false);
			}
     	} else {
			if (namesNUplet.size()==0) {
    	    	from.addQuantifierName(new RAWSQLQuery("{0=0}",true),new RAWSQLQuery(t4.image,true),namesNUplet,isFirstQuantifier,new Integer("-1"),false);
     		} else {
        		from.addQuantifierName(new RAWSQLQuery("{0=0}",true),new RAWSQLQuery(t4.image,true),namesNUplet,isFirstQuantifier,new Integer("-1"),false);
			}
    	}
    } else {
		if (t4==null) {
			if (namesNUplet.size()==0) {
        		from.addQuantifierName(new RAWSQLQuery(t3.image,true),new RAWSQLQuery("{0=0}",true),namesNUplet,isFirstQuantifier,new Integer("-1"),false);
     		} else {
        		from.addQuantifierName(new RAWSQLQuery(t3.image,true),new RAWSQLQuery("{0=0}",true),namesNUplet,isFirstQuantifier,new Integer("-1"),false);
			}
     	} else {
			if (namesNUplet.size()==0) {
        		from.addQuantifierName(new RAWSQLQuery(t3.image,true),new RAWSQLQuery(t4.image,true),namesNUplet,isFirstQuantifier,new Integer("-1"),false);
     		} else {
        		from.addQuantifierName(new RAWSQLQuery(t3.image,true),new RAWSQLQuery(t4.image,true),namesNUplet,isFirstQuantifier,new Integer("-1"),false);
			}
    	}
    }} 
 }

void AtleastPercentQuantifier(From from, boolean isFirstQuantifier, Token nValue):
{
  Token t = null;
  Token t2 = null;
  Token t3 = null;
  Token t4 = null;
  ArrayList<String> namesNUplet = new ArrayList<String>(); 
}
{ 
 (<OF> (AjoutNamesNUplet(namesNUplet) | "("AjoutNamesNUplet(namesNUplet) ("," AjoutNamesNUplet(namesNUplet))+")") (<WHERE> t3=< RAW_SQL_STATEMENT >)? <COLON> (t4=< RAW_SQL_STATEMENT > < AND >)?
    { if (t3==null) { 
		if (t4==null) {
			if (namesNUplet.size()==0) {
        		from.addQuantifierName(new RAWSQLQuery("{0=0}",true),new RAWSQLQuery("{0=0}",true),namesNUplet,isFirstQuantifier,new Integer(nValue.image),true);
     		} else {
        		from.addQuantifierName(new RAWSQLQuery("{0=0}",true),new RAWSQLQuery("{0=0}",true),namesNUplet,isFirstQuantifier,new Integer(nValue.image),true);
			}
     	} else {
			if (namesNUplet.size()==0) {
        		from.addQuantifierName(new RAWSQLQuery("{0=0}",true),new RAWSQLQuery(t4.image,true),namesNUplet,isFirstQuantifier,new Integer(nValue.image),true);
     		} else {
        		from.addQuantifierName(new RAWSQLQuery("{0=0}",true),new RAWSQLQuery(t4.image,true),namesNUplet,isFirstQuantifier,new Integer(nValue.image),true);
			}
    	}
    } else {
		if (t4==null) {
			if (namesNUplet.size()==0) {
        		from.addQuantifierName(new RAWSQLQuery(t3.image,true),new RAWSQLQuery("{0=0}",true),namesNUplet,isFirstQuantifier,new Integer(nValue.image),true);
     		} else {
        		from.addQuantifierName(new RAWSQLQuery(t3.image,true),new RAWSQLQuery("{0=0}",true),namesNUplet,isFirstQuantifier,new Integer(nValue.image),true);
			}
     	} else {
			if (namesNUplet.size()==0) {
        		from.addQuantifierName(new RAWSQLQuery(t3.image,true),new RAWSQLQuery(t4.image,true),namesNUplet,isFirstQuantifier,new Integer(nValue.image),true);
     		} else {
        		from.addQuantifierName(new RAWSQLQuery(t3.image,true),new RAWSQLQuery(t4.image,true),namesNUplet,isFirstQuantifier,new Integer(nValue.image),true);
			}
    	}
    }})
  }

 void AtleastNQuantifier(From from, boolean isFirstQuantifier, Token nValue):
{
  Token t = null;
  Token t2 = null;
  Token t3 = null;
  Token t4 = null;
  ArrayList<String> namesNUplet = new ArrayList<String>(); 
}
{ 
 (<OF> (AjoutNamesNUplet(namesNUplet) | "("AjoutNamesNUplet(namesNUplet) ("," AjoutNamesNUplet(namesNUplet))+")") (<WHERE> t3=< RAW_SQL_STATEMENT >)? <COLON> (t4=< RAW_SQL_STATEMENT > < AND >)?
    { if (t3==null) { 
		if (t4==null) {
			if (namesNUplet.size()==0) {
        		from.addQuantifierName(new RAWSQLQuery("{0=0}",true),new RAWSQLQuery("{0=0}",true),namesNUplet,isFirstQuantifier,new Integer(nValue.image),false);
     		} else {
        		from.addQuantifierName(new RAWSQLQuery("{0=0}",true),new RAWSQLQuery("{0=0}",true),namesNUplet,isFirstQuantifier,new Integer(nValue.image),false);
			}
     	} else {
			if (namesNUplet.size()==0) {
        		from.addQuantifierName(new RAWSQLQuery("{0=0}",true),new RAWSQLQuery(t4.image,true),namesNUplet,isFirstQuantifier,new Integer(nValue.image),false);
     		} else {
        		from.addQuantifierName(new RAWSQLQuery("{0=0}",true),new RAWSQLQuery(t4.image,true),namesNUplet,isFirstQuantifier,new Integer(nValue.image),false);
			}
    	}
    } else {
		if (t4==null) {
			if (namesNUplet.size()==0) {
        		from.addQuantifierName(new RAWSQLQuery(t3.image,true),new RAWSQLQuery("{0=0}",true),namesNUplet,isFirstQuantifier,new Integer(nValue.image),false);
     		} else {
        		from.addQuantifierName(new RAWSQLQuery(t3.image,true),new RAWSQLQuery("{0=0}",true),namesNUplet,isFirstQuantifier,new Integer(nValue.image),false);
			}
     	} else {
			if (namesNUplet.size()==0) {
        		from.addQuantifierName(new RAWSQLQuery(t3.image,true),new RAWSQLQuery(t4.image,true),namesNUplet,isFirstQuantifier,new Integer(nValue.image),false);
     		} else {
        		from.addQuantifierName(new RAWSQLQuery(t3.image,true),new RAWSQLQuery(t4.image,true),namesNUplet,isFirstQuantifier,new Integer(nValue.image),false);
			}
    	}
    }})
  }
  
void AjoutNamesNUplet(ArrayList<String > namesNUplet):
{  Token t = null;
}
{	t=<IDENTIFIER>
	{		namesNUplet.add(t.image);
	}
}
      
SQLBooleanValue SQLBooleanValue():
{ SQLBooleanValue v;}
{
  v=SQLOr() { return v; }
}

SQLBooleanValue SQLOr():
{ SQLBooleanValue a, b = null; }
{
  a=SQLAnd() ( <OR> b= SQLOr() ) ? 
  {
    return b == null ? a : SQLBooleanOp.or(a,b);
  }
}

SQLBooleanValue SQLAnd():
{ SQLBooleanValue a, b = null; }
{
  a=AtomicSQLBooleanValue() ( <AND> b= SQLAnd() ) ? 
  {
    return b == null ? a : SQLBooleanOp.and(a,b);
  }
}

SQLBooleanValue AtomicSQLBooleanValue():
{
  SQLBooleanValue v = null;
  Token t;
}
{
  ( t=< RAW_SQL_STATEMENT > { return new RAWSQLAtom(t.image,true);} )
| ( "(" v=SQLBooleanValue() ")" { return v; } )
}